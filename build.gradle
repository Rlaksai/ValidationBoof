
group = 'org.boofcv'
version = '0.29'

allprojects{
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    dependencies {
        compile files("$project.rootDir/lib/ProjectBUBO.jar")

        ['boofcv-core','boofcv-swing'].each { String a -> compile group: 'org.boofcv', name: a, version: '0.29-SNAPSHOT' }
        compile group: 'com.google.guava', name: 'guava', version: '17.0'
        compile group: 'commons-io', name: 'commons-io', version: '2.6'
        compile group: 'args4j', name: 'args4j', version: '2.33'

        compile 'com.sun.mail:javax.mail:1.6.0'

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

subprojects {
    // Create a jar which contains all the applications
    task moduleJars(type: Jar) {
        manifest {
            attributes 'Implementation-Title': 'BoofCV Regression Jar',
                    'Implementation-Version': '1'
        }

        outputs.upToDateWhen { false }

        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar

        doLast {
            archivePath.renameTo(file('module_regression.jar'))
        }
    }

    clean.doFirst {
        delete "module_regression.jar"
    }
}

[':modules'].each {String a ->
    project(a) {
        jar.enabled = false
        install.enabled = false
        moduleJars.enabled = false
    }
}

clean.doFirst {
    delete "results"
    delete "tmp"
}

dependencies {
    compile project(':modules:common')
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
}

