apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'

group = 'org.boofcv'
version = '0.28'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'test/java'
        }
    }
}

task selectJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Validation Boof',
                'Implementation-Version': version,
                'Main-Class': 'validate.applications.HandSelectQRCodeApp'
    }

    outputs.upToDateWhen { false }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    doLast {
        archivePath.renameTo(file('select.jar'))
    }
}

dependencies {
    compile files("lib/ProjectBUBO.jar")

    ['boofcv-core','boofcv-swing'].each { String a -> compile group: 'org.boofcv', name: a, version: '0.28-SNAPSHOT' }

    compile group: 'com.google.guava', name: 'guava', version: '17.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

idea {
    project {
        jdkName = '1.8 (64bit)'
        languageLevel = '1.8'
    }

    module {
        name = "BoofCV Validation"
    }
}

